OnMouseDoubleClick
{
	if reference5 == 0 && reference3 == 0 //left click on sakura
	{
		OnSakuraMenu
	}
}

OnSakuraMenu : all
{
	"\0\s[0]\b[0]\![set,autoscroll,disable]\_q"
	
	"\![*]\q[Talk,OnAiTalk]  "
	switch LastTalk == ""
	{
		"\![*]\q[Repeat,OnLastTalk]"
		"\![*]\f[color,disable]Repeat\f[color,default]"
	}
	
	"\n\n"
	
	"\![*]\q[Change talkrate,OnTalkRateMenu]\n\n"
	
	"\![*]\q[Cancel,blank]"
	
	"\n\n"
	
	if FullyOriented //Bug protection...
	{
		"(Memories complete.)"
	}
	elseif NeedsRemindedOfGarden
	{
		"(Need to remember this place...)"
	}
	elseif NeedsRemindedOfHerName
	{
		"(Need to remember her name...)"
	}
	elseif NeedsRemindedOfUserName
	{
		"(Need to remember your name...)"
	}
	else //FullyOriented
	{
		"(Memories complete.)"
	}
}

OnLastTalk
{
	LastTalk
}

OnTalkRateMenu : all
{
	"\0\![set,autoscroll,disable]\![no-autopause]\_q"
	"Talkrate:\n"
	_times = ("Off,0","1m,60","3m,180","5m,300","10m,600","15m,900")
	foreach _times; _time
	{
		switch TOINT(_time[1]) == aitalkinterval
		{
			"\__q[OnTalkRateChange,%(_time[1])]%(_time[0])\__q"
			"\f[underline,1]\_a[OnTalkRateChange,%(_time[1])]%(_time[0])\_a\f[underline,default]"
		}
		"  "
	}
	"\n\n"
	"\![*]\q[Back,OnSakuraMenu]  \![*]\q[Cancel,blank]"
}

OnTalkRateChange
{
	aitalkinterval = TOINT(reference0)
	OnTalkRateMenu
}